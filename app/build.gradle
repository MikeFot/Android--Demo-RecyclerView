apply plugin: 'com.android.application'

final int versionMajor = 1
final int versionMinor = 0
final int versionPatch = 0
final String semanticVersion = "${versionMajor}.${versionMinor}.${versionPatch}"
final String appCompatLibVersion = "24.0.0-alpha2"

final int targetSdk = rootProject.targetSdkVersion;
final int minSdkRed = rootProject.minSdkVersion;

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    packagingOptions.excludes = getDefaultExcludes();

    defaultConfig {
        applicationId "com.michaelfotiadis.demorecyclerview"

        minSdkVersion minSdkRed
        targetSdkVersion targetSdk

        versionCode computeVersionCode()
        versionName semanticVersion

        proguardFiles getDefaultProguardFile('proguard-android.txt'),
                'proguard/gson.pro',
                'proguard/butterknife.pro'


        buildConfigField "String", "GIT_SHA", quoteString(gitSha())
        buildConfigField "String", "BUILD_TIMESTAMP", quoteString(getNow())
        buildConfigField "String", "GIT_TIMESTAMP", quoteString(gitTimestamp())


    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        prod {
            applicationId "com.michaelfotiadis.demorecyclerview"
            resValue("string", "app_name", "Demo App")
        }
        dev {
            applicationId "com.michaelfotiadis.demorecyclerview.debug"
            resValue("string", "app_name", "Demo App Dev")
        }
    }


    buildTypes {

        release {
            minifyEnabled false
            debuggable false

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "Boolean", "DEV_MODE", "false"
        }

        debug {
            testCoverageEnabled true
            debuggable true
            buildConfigField "Boolean", "DEV_MODE", "true"
        }

        applicationVariants.all { final variant ->
            variant.outputs.each { final output ->
                annotateApkFilename(variant, output);
            }
        }

    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile project(':core')

    compile 'com.jakewharton:butterknife:7.0.1'
    compile "com.android.support:appcompat-v7:$appCompatLibVersion"
    compile "com.android.support:design:$appCompatLibVersion"
    compile "com.android.support:cardview-v7:$appCompatLibVersion"
    compile "com.android.support:recyclerview-v7:$appCompatLibVersion"

    compile 'com.android.support:appcompat-v7:23.4.0'


    testCompile 'junit:junit:4.12'
}
